info analysis
  title "Review of the 13 TeV ATLAS Open Data release"
  experiment ATLAS
  id "ATLOREACH-PUB-2020-001"
  publication " EPJ Web Conf., 245 (2020) 08026"
  sqrtS 13.0
  lumi 10000
  doi "10.1051/epjconf/202024508026"


### Some Issues
## lep_theta is recognized as lep_eta by ADL/Cutlang

SkipHistos = 1

# OBJECT SELECTION

object goodEles
  take ELE
  select lep_isTightID(ELE) == 1 
  select lep_pt(ELE) > 25000.0
  select lep_ptcone30(ELE)/lep_pt(ELE) <   0.15       
  select lep_etcone20(ELE)/lep_pt(ELE) <   0.15      
  select abs(lep_eta(ELE)) <   2.47        
  select abs(lep_eta(ELE)) ][ 1.37 1.52   
  select Abs(lep_trackd0pvunbiased(ELE))/lep_tracksigd0pvunbiased(ELE) < 5.0
  select Abs(lep_z0(ELE))*sin(dz(ELE))< 0.5
# dz is Theta of the vector

object goodMuos
  take MUO
  select lep_isTightID(MUO) == 1 
  select lep_pt(MUO) > 25000.0                  
  select lep_ptcone30(MUO)/lep_pt(MUO) <   0.15       
  select lep_etcone20(MUO)/lep_pt(MUO) <   0.15      
  select abs(lep_eta(MUO)) < 2.5 
  select Abs(lep_trackd0pvunbiased(MUO))/lep_tracksigd0pvunbiased(MUO) < 3.0
  select Abs(lep_z0(MUO)*sin(dz(MUO))) < 0.5

object goodLeps : Union(goodEles, goodMuos)

## VARIABLE DEFINITIONS

define leadLep = goodLeps[0]
define subleadLep = goodLeps[1]
define Zll = leadLep subleadLep

#define scaleFactor = scaleFactor_ELE*scaleFactor_MUON*scaleFactor_LepTRIGGER*scaleFactor_PILEUP
define totWeight =  mcWeight*scaleFactor/Abs(mcWeight)
#define totWeight = 1.0

histoList leadlepHistos
  histo hleadleptPt,"Leading Lepton Transverse Momentum;p_{T}^{leadlep}[GeV];Events/bin ", 20, 0, 200, Pt(leadLep)
  histo hleadleptEta,"Leading Lepton Pseudorapidity; #eta^{leadlep}; Events / bin", 30, -3, 3, Eta(leadLep)
  histo hleadleptE, "Leading Lepton Energy; E^{leadlep} [GeV]; Events / bin", 30, 0, 300, E(leadLep)
  histo hleadleptPhi, "Leading Lepton Azimuthal Angle ; #phi^{leadlep}; Events / bin", 32, -3.2, 3.2, Phi(leadLep)
  histo hleadleptCh, "Leading Lepton Charge; Q^{leadlep}; Events / bin", 7, -1.75, 1.75, q(leadLep) 
  histo hleadleptID, "Leading Lepton Absolute PDG ID; |PDG ID|^{leadlep}; Events / bin",  15, 5.5, 20.5, pdgID(leadLep)

## EVENT SELECTION

region Zll
  select ALL
  select trigE == 1 || trigM == 1
  select Size(goodEles) >= 0
  select Size(goodMuos) >= 0
  select Size(goodLeps) >= 0
  sort Pt(goodLeps) descend
  select Size(goodLeps) == 2
  select Size(JET) == 0
  select q(leadLep)*q(subleadLep) < 0
  select Abs(pdgID(leadLep)) == Abs(pdgID(subleadLep))
  select Abs(m(Zll) - 91.18 ) < 25
  weight myweight totWeight
  leadlepHistos
  histo hmLL, "Mass of Dilepton System; m_{ll} [GeV];Events / bin", 30, 68.0, 114.0, m(Zll)

region Zee
  Zll
  select Abs(pdgID(leadLep)) == 11
  histo heemLL, "Mass of Dilepton System; m_{ll} [GeV];Events / bin", 30, 68.0, 114.0, m(Zll)

region Zmm
  Zll
  select Abs(pdgID(leadLep)) == 13
  histo hmmmLL, "Mass of Dilepton System; m_{ll} [GeV];Events / bin", 30, 68.0, 114.0, m(Zll)
